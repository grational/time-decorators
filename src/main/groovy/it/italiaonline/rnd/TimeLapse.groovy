package it.italiaonline.rnd.time

import groovy.transform.CompileStatic

/**
 * This Groovy source file was auto generated by 'gigawatt'
 * @author gsus
 * @date 15-12-2016 12.55
 */
@CompileStatic
class TimeLapse {
	private final String input

	TimeLapse(String input) {
		this.input = input
	}

	/**
	 * Return the timelapse value in milliseconds
	 */
	BigInteger ms()
	throws IllegalArgumentException {

		BigInteger secondInMs = 1000
		BigInteger minuteInMs = 60 * secondInMs
		BigInteger hourInMs   = 60 * minuteInMs
		BigInteger dayInMs    = 24 * hourInMs
		BigInteger strippedInput = this.input.replaceAll(/\D/,'') as BigInteger

		BigInteger result
		switch(this.input) {
			case ~/\d+s?/:
				result = strippedInput * secondInMs
				break
			case ~/\d+m/:
				result = strippedInput * minuteInMs
				break
			case ~/\d+h/:
				result = strippedInput * hourInMs
				break
			case ~/\d+d/:
				result = strippedInput * dayInMs
				break
			default:
				throw new IllegalArgumentException("The input '$input' does not satisfy the rule /^\\d+[smhd]?\$/")
		}
		return result
	}
}
